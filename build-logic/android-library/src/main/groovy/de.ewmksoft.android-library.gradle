plugins {
    id('com.android.library')
    id('maven')
    id('signing')
}

signing {
    sign configurations.archives
}

//version = '1.4.0-SNAPSHOT'
version = '1.4.0'

android {
    compileSdkVersion 23
    
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName version
        
        setProperty("archivesBaseName", "xyplot-android")
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'InvalidPackage', 'OldTargetApi'
    }    
    
    
    libraryVariants.all { variant ->
        variant.outputs.all { output ->
            if (outputFile != null && outputFileName.endsWith('.aar')) {
                outputFileName = "${archivesBaseName}-${version}.aar"
            }
        }
    }

    sourceCompatibility = '1.6'
    targetCompatibility = '1.6'


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }
}

sourceSets {
    main {
        java {
            android.sourceSets.main.java.srcDir '../../core/xyplot/src/main/java'
        }
    }
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    baseName = 'xyplot-android'
    from android.sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    baseName = 'xyplot-android'
    from files("$buildDir/docs/javadoc")
}

task createJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += files("$buildDir/intermediates/compile_r_class_jar/release/R.jar")
    exclude "**/R**"
    failOnError = false
}


artifacts {
    archives androidSourcesJar, javadocJar
}

javadocJar.dependsOn createJavadocs

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')    
}
